#!/bin/env lang3

# Conway's Game of Life

let WIDTH = 40
let HEIGHT = 40
let GENERATIONS = 200

let WIDTH_RANGE = range(WIDTH)
let HEIGHT_RANGE = range(HEIGHT)

fn create_board()
  let mut board = []
  for i in HEIGHT_RANGE do
    let mut row = []
    for j in WIDTH_RANGE do
      row += [false]
    end
    board += [row]
  end
  return board
end

fn for_cell_in_board(board, f)
  for i in HEIGHT_RANGE do
    for j in WIDTH_RANGE do
      f(i, j)
    end
  end
end

fn randomize_board(board)
  for_cell_in_board(board, fn(i, j)
    # make ~30% cells alive
    board[i][j] = random(10) < 3
  end)
end

let NEIGHBOR_RANGE = range(-1, 2)

fn count_neighbors(board, row, col)
  let mut count = 0

  for i in NEIGHBOR_RANGE do
    for j in NEIGHBOR_RANGE do
      # Skip the cell itself
      if (i == j == 0) then continue end

      let ni = row + i
      let nj = col + j

      count += int(
        0 <= ni < HEIGHT and
        0 <= nj < WIDTH and
        board[ni][nj]
      )
    end
  end

  return count
end

fn next_generation(board)
  let mut new_board = create_board()

  for_cell_in_board(board, fn(i, j)
      let neighbors = count_neighbors(board, i, j)
      # Game of Life rules:
      # 1. Any live cell with 2-3 neighbors survives
      # 2. Any dead cell with exactly 3 neighbors becomes alive
      # 3. All other cells die or stay dead
      new_board[i][j] = (
          (board[i][j] and neighbors == 2) or neighbors == 3
      )
  end)

  return new_board
end

let HORIZONTAL_BORDER = "+" + 2 * WIDTH * "-" + "+"

let FULL_BLOCK = "██"
let EMPTY_BLOCK = "  "

fn display_board(board)
  println(HORIZONTAL_BORDER)

  for row in board do
    print("|")
    for cell in row do
      if cell then
        print(FULL_BLOCK)
      else
        print(EMPTY_BLOCK)
      end
    end
    println("|")
  end

  println(HORIZONTAL_BORDER)
end

# Main simulation
fn main()
  let mut board = create_board()
  randomize_board(board)

  for gen in range(GENERATIONS) do
    println("Conway's Game of Life - Generation:", str(gen))
    println()
    display_board(board)
    board = next_generation(board)
  end

  println()
  println("Simulation complete!")
end

main()
