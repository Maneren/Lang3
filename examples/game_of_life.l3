#!/bin/env lang3

# Conway's Game of Life

let WIDTH = int(input("Enter the width of the board: "))
let HEIGHT = int(input("Enter the height of the board: "))
let GENERATIONS = int(input("Enter the number of generations: "))

fn create_board()
  let mut board = []
  for i in 0..HEIGHT do
    let mut row = []
    for j in 0..WIDTH do
      row += [false]
    end
    board += [row]
  end
  return board
end

fn for_cell_in_board(board, f)
  for i in 0..HEIGHT do
    for j in 0..WIDTH do
      f(i, j)
    end
  end
end

fn randomize_board(board)
  for_cell_in_board(board, fn(i, j)
    # make ~30% cells alive
    board[i][j] = random(10) < 3
  end)
end

fn count_neighbors(board, row, col)
  let mut count = 0

  for i in row - 1 ..= row + 1 do
    for j in col - 1 ..= col + 1 do
      # Skip the cell itself
      if i == row and j == col then continue end

      count += int(
        0 <= i < HEIGHT and
        0 <= j < WIDTH and
        board[i][j]
      )
    end
  end

  return count
end

fn next_generation(board)
  let mut new_board = create_board()

  for_cell_in_board(board, fn(i, j)
      let neighbors = count_neighbors(board, i, j)
      # Game of Life rules:
      # 1. Any live cell with 2-3 neighbors survives
      # 2. Any dead cell with exactly 3 neighbors becomes alive
      # 3. All other cells die or stay dead
      new_board[i][j] = (
          (board[i][j] and neighbors == 2) or neighbors == 3
      )
  end)

  return new_board
end

let HORIZONTAL_BORDER = "+" + 2 * WIDTH * "-" + "+"

let FULL_BLOCK = "██"
let EMPTY_BLOCK = "  "

fn display_board(board)
  println(HORIZONTAL_BORDER)

  for row in board do
    print("|")
    for cell in row do
      if cell then
        print(FULL_BLOCK)
      else
        print(EMPTY_BLOCK)
      end
    end
    println("|")
  end

  println(HORIZONTAL_BORDER)
end

# Main simulation
fn main()
  let mut board = create_board()
  randomize_board(board)

  for gen in 0..GENERATIONS do
    println("Conway's Game of Life - Generation:", str(gen))
    println()
    display_board(board)
    board = next_generation(board)
  end

  println()
  println("Simulation complete!")
end

main()
