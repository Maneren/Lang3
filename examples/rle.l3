#!/bin/env lang3
# Run-length encoding decoder
#
# Reads a run-length encoded string from stdin and prints the decoded string to
# stdout
#
# Example:
#    $ echo "2[a2[bc]]3[cd]ef" | rle.l3
#    abcbcabcbccdcdcdef

fn load_brackets_inner(depth, acc, data)
  if not data then
    error("invalid input")
  end

  let x, xs = head(data)

  if x == "[" then
    depth += 1
  elif x == "]" then
    if depth == 0 then
      return [acc, xs]
    end

    depth -= 1
  end

  return load_brackets_inner(depth, acc + x, xs)
end

let load_brackets = load_brackets_inner(0, "")

fn rle_decoder(data)
  if not data then return data end

  let x, xs = head(data)

  if "0" <= x and x <= "9" then
    if not xs then error("invalid input") end

    let count = int(x)
    let y, ys = head(xs)

    if y == "[" then y, ys = load_brackets(ys) end

    return count * rle_decoder(y) + rle_decoder(ys)
  elif x == "[" then
    let y, ys = load_brackets(data)
    return rle_decoder(y) + rle_decoder(ys)
  end

  return x + rle_decoder(xs)
end

println(rle_decoder(input()))
