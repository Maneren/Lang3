#!/bin/env lang3

# Simple Blackjack CLI Game

fn create_deck()
  let suits = ["♠", "♥", "♦", "♣"]
  let ranks = ["A", "2", "3", "4", "5", "6", "7", "8", "9", "X", "J", "Q", "K"]
  let mut deck = []

  let mut i = 0
  while i < 4 do
    let mut j = 0
    while j < 13 do
      deck += [ranks[j] + suits[i]]
      j += 1
    end
    i += 1
  end

  return deck
end

fn swap(arr, i, j)
  let temp = arr[i]
  arr[i] = arr[j]
  arr[j] = temp
end

fn shuffle_deck(deck)
  let mut i = len(deck) - 1
  while i > 0 do
    swap(deck, i, random(i))
    i -= 1
  end
end

fn shuffled_deck()
  let deck = create_deck()
  shuffle_deck(deck)
  return deck
end

let deal = head

fn card_value(card)
  let rank, suit = head(card)

  if rank == "A" then return 11 end
  if rank == "X" or rank == "J" or rank == "Q" or rank == "K" then return 10 end

  return int(rank)
end

fn hand_value(hand)
  let mut total = sum(map(card_value, hand))
  let mut aces = count(fn(card) return card[0] == "A" end, hand)

  # Adjust for aces
  while total > 21 and aces > 0 do
    total -= 10
    aces -= 1
  end

  return total
end

fn show_hand(name, hand, hide_first)
  print(name + ": ")

  let mut i = 0
  while i < len(hand) do
    if i == 0 and hide_first then
      print("?? ")
    else
      print(hand[i] + " ")
    end
    i += 1
  end

  println(
    if not hide_first then
      return "(Value: " + str(hand_value(hand)) + ")"
    else
      return ""
    end
  )
end

fn small_delay() sleep(1000) end
fn long_delay() sleep(2500) end

fn main()
  println("BLACKJACK")
  println()

  let MAX_VALUE = 21

  let mut wins = 0
  let mut losses = 0

  let mut deck = create_deck()

  while true do
    shuffle_deck(deck)
    println(deck)

    # Deal initial cards
    let mut player_card1, player_card2, dealer_card1, dealer_card2
    player_card1, deck = deal(deck)
    dealer_card1, deck = deal(deck)
    player_card2, deck = deal(deck)
    dealer_card2, deck = deal(deck)

    let mut player_hand = [player_card1, player_card2]
    let mut dealer_hand = [dealer_card1, dealer_card2]

    # Show initial hands
    show_hand("Dealer", dealer_hand, true)
    show_hand("Player", player_hand, false)
    println()

    # Player's turn
    let mut player_value = hand_value(player_hand)
    let mut card
    while player_value < MAX_VALUE do
      if input("Hit or Stand? (H/s): ") == "s" then break end
      println()

      card, deck = deal(deck)
      player_hand += [card]
      show_hand("Player", player_hand, false)
      player_value = hand_value(player_hand)
      small_delay()
      println()
    end

    # Check if player busted
    if player_value > MAX_VALUE then
      println("Player busts! Dealer wins!")
      losses += 1
    else
      # Dealer's turn
      println("Dealer reveals hand:")
      show_hand("Dealer", dealer_hand, false)
      small_delay()
      println()

      let mut dealer_value = hand_value(dealer_hand)

      while dealer_value < 17 or dealer_value < player_value do
        println("Dealer hits...")

        card, deck = deal(deck)
        dealer_hand += [card]
        show_hand("Dealer", dealer_hand, false)
        dealer_value = hand_value(dealer_hand)
        long_delay()
        println()
      end

      # Determine winner
      if dealer_value > 21 then
        println("Dealer busts! Player wins!")
        wins += 1
      elif player_value >= dealer_value then
        println("Player wins!")
        wins += 1
      elif dealer_value > player_value then
        println("Dealer wins!")
        losses += 1
      end
    end
    println()

    deck += player_hand + dealer_hand

    println("Score:")
    println("\tPlayer:", wins)
    println("\tDealer:", losses)
    println()

    if input("Play again? (y/N): ") != "y" then
      break
    end
  end
end

main()
